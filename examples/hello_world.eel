// REAPER 6.24 or newer only! (Use the legacy syntax extension_api(...) for older versions)

ctx = ImGui_CreateContext("Hello World!");
ImGui_NumericLimits_Float(FLT_MIN, FLT_MAX);

color = 0;
#notes = "";

function frame() (
  ImGui_TextWrapped(ctx, "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Ut ultrices turpis lectus, in semper ligula blandit in. Duis non ipsum varius, pretium magna eu, mattis sapien. Maecenas augue mi, luctus sed leo et, ullamcorper convallis ligula. Nunc vitae porttitor quam. Integer a lorem fermentum, dapibus tortor ut, gravida erat. Quisque ut justo ante. Quisque fringilla a lectus a porta.");
  ImGui_Spacing(ctx);

  track = GetSelectedTrack(0, 0);

  track ? (
    GetTrackGUID(#guid, track);
    ImGui_PushID(ctx, #guid);
  ) : (
    color = 0;
    #notes = "";
    ImGui_BeginDisabled(ctx);
  );

  color = ImGui_ColorConvertNative(GetTrackColor(track));
  ImGui_ColorEdit3(ctx, "track color", color) ?
    SetTrackColor(track, ImGui_ColorConvertNative(color));
  ImGui_Spacing(ctx);

  GetSetMediaTrackInfo_String(track, "P_EXT:imhw_notes", #notes, 0);
  ImGui_InputTextMultiline(ctx, "track notes", #notes, 0, -FLT_MIN) ?
    GetSetMediaTrackInfo_String(track, "P_EXT:imhw_notes", #notes, 1);

  track ? ImGui_PopID(ctx) : ImGui_EndDisabled(ctx);
);

function loop() (
  open = 1; // enable the close button
  ImGui_SetNextWindowSize(ctx, 500, 420, ImGui_Cond_FirstUseEver());
  ImGui_Begin(ctx, "Hello World!", open) ? ( frame(); ImGui_End(ctx); );
  open ? defer("loop()")
);

defer("loop()");

