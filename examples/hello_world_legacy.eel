// For compatibility with REAPER versions prior 6.24.
// See hello_world.eel (ReaImGui_Hello World.eel) for an example using the new extension API syntax.

ctx = extension_api("ImGui_CreateContext", "Hello World!");
extension_api("ImGui_NumericLimits_Float", FLT_MIN, FLT_MAX);

color = 0;
#notes = "";

function frame() (
  extension_api("ImGui_TextWrapped", ctx, "Lorem ipsum dolor sit amet, consectetur adipiscing elit. Ut ultrices turpis lectus, in semper ligula blandit in. Duis non ipsum varius, pretium magna eu, mattis sapien. Maecenas augue mi, luctus sed leo et, ullamcorper convallis ligula. Nunc vitae porttitor quam. Integer a lorem fermentum, dapibus tortor ut, gravida erat. Quisque ut justo ante. Quisque fringilla a lectus a porta.");
  extension_api("ImGui_Spacing", ctx);

  track = GetSelectedTrack(0, 0);

  track ? (
    GetTrackGUID(#guid, track);
    extension_api("ImGui_PushID", ctx, #guid);
  ) : (
    color = 0;
    #notes = "";
    extension_api("ImGui_BeginDisabled", ctx);
  );

  color = extension_api("ImGui_ColorConvertNative", GetTrackColor(track));
  extension_api("ImGui_ColorEdit3", ctx, "track color", color) ?
    SetTrackColor(track, extension_api("ImGui_ColorConvertNative", color));
  extension_api("ImGui_Spacing", ctx);

  GetSetMediaTrackInfo_String(track, "P_EXT:imhw_notes", #notes, 0);
  extension_api("ImGui_InputTextMultiline", ctx, "track notes", #notes, 0, -FLT_MIN) ?
    GetSetMediaTrackInfo_String(track, "P_EXT:imhw_notes", #notes, 1);

  track
    ? extension_api("ImGui_PopID", ctx)
    : extension_api("ImGui_EndDisabled", ctx);
);

function loop() (
  open = 1; // enable the close button
  extension_api("ImGui_SetNextWindowSize", ctx, 500, 420, extension_api("ImGui_Cond_FirstUseEver"));
  extension_api("ImGui_Begin", ctx, "Hello World!", open) ? (
    frame();
    extension_api("ImGui_End", ctx);
  );
  open ? defer("loop()");
);

defer("loop()");
